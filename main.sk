command skr [<text>]:
    description: Reload de skripts
    trigger:
        if player is op:
            if arg 1 is set:
                execute player command "sk reload %text-arg%"
            else if arg 1 isn't set:
                execute player command "sk reload main" # sk reload all pour tout reload
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command broadcast [<text>]:
    description: Fais une annonce (30s de cooldown)
    cooldown: 30 seconds	
    cooldown message: &4Tu dois attendre &l%remaining time% &4pour refaire cette commande !
    aliases: /bc
    trigger:
        if player is op:
            if arg 1 is set:
                broadcast "&a&lBREAKING NEWS &7» &6%arg 1%"
            else:
                send "&4Vous n'avez pas spécifier de texte."
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command skull [<offline player>]:
    aliases: head, tete
    description: Te drop une tête d'un joueur
    executable by: players
    trigger:
        if player is op:
            if arg 1 isn't set:
                give player's skull to player
                send "&7Vous avez reçu la tête de : &6%player%"
            if arg 1 is set:
                give arg's skull to player
                send "&7Vous avez reçu la tête de : &6%arg 1%"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command clearchat:
    aliases: cc
    description: Efface l'historique du chat pour tout le monde (1mn de cooldown)
    cooldown: 1 minute
    cooldown message: &4Tu dois attendre &l%remaining time% &4pour refaire cette commande !
    trigger:
        if player is op:
            loop 150 times:
                broadcast " "
            broadcast "&cLe Chat a été clear par &e%player%&c."
            broadcast " "
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command forceexecute <player> <text>:
    aliases: /fe
    description: Force une personne à executer une commande
    trigger:
        if player is op:
            execute arg-1 command arg-2
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

# on weather change to rain or thunder: # soleil pour toujours
# 	cancel event

command sethome [<text>]:
    description: Défini un point de téléportation pour ta maison
    executable by: players
    trigger:
        if text-arg isn't set:
            set {_name} to "maison"
        else:
            set {_name} to text-arg
        if {home.%player's uuid%.%{_name}%} is set:
            if text-arg isn't set:
                set {_name} to ""
            else:
                set {_name} to "%{_name}% "
            send "&4Cette maison existe déjà, &8/delhome %{_name}%&4pour la supprimer."
        else:
            remove {_name} from {home.%player's uuid%::*}
            add {_name} to {home.%player's uuid%::*}
            set {home.%player's uuid%.%{_name}%} to player's location
            send "Ta maison ""&a%{_name}%&r"" est maintenant définie aux coordonnées &7%player's location%&r !"

command delhome [<text>]:
    description: Supprime le tp vers ta maison
    executable by: players
    trigger:
        if text-arg isn't set:
            set {_name} to "maison"
        else:
            set {_name} to text-arg
        if {home.%player's uuid%.%{_name}%} is set:
            remove {_name} from {home.%player's uuid%::*}
            clear {home.%player's uuid%.%{_name}%}
            send "Maison ""&a%{_name}%&r"" supprimée."
        else:
            send "&4Cette maison n'existe pas."
            make player execute command "listhome"

command home [<text>]:
    description: Te téléportes à ta maison
    executable by: players
    trigger:
        if text-arg isn't set:
            set {_name} to "maison"
        else:
            set {_name} to text-arg
        if {home.%player's uuid%.%{_name}%} is set:
            send "&aTéléportation vers &9%{_name}% &a!"
            teleport player to {home.%player's uuid%.%{_name}%}
        else:
            send "&4Le home ""&c%{_name}%"" &4n'existe pas."
            make player execute command "listhome"

on tab complete for "home" and "delhome":
    if {home.%player's uuid%::*} is set:
        set tab completions for position 1 to the first element of {home.%player's uuid%::*}
        loop {home.%player's uuid%::*}:
            if loop-value isn't the first element of {cards.%player%::*}:
                add loop-value to tab completions

command listhome:
    description: Liste des tes maisons
    executable by: players
    trigger:
        if {home.%player's uuid%::*} isn't set:
            send "&4Vous n'avez pas de maisons."
        else:
            set {_maisons} to "%{home.%player's uuid%::*}%"
            replace all "and" with "et" in {_maisons}
            send "&aListe des tes maisons : %{_maisons}%"

command rename <string>:
    description: Renomme l'item que tu as dans ta main
    executable by: players
    trigger:
        if player is op:
            if player's tool is air:
                send "&4Tu dois avoir un item dans la main pour le renommer."
            else:
                set {_name} to arg
                replace all "&" with "§" in {_name}
                send "&eL'item &r%player's tool%&e est renommé en ""&r%{_name}%&e""."
                set the name of the player's tool to "§r%{_name}%"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."
