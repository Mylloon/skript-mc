command skr [<text>]: # recharge ce skript
    executable by: players, console
    description: Reload de skripts
    trigger:
        if arg-1 is set:
            execute player command "sk reload %arg-1%"
        else if arg-1 isn't set:
            execute player command "sk reload main" # sk reload all pour tout reload

command broadcast [<text>]: # annonce
    executable by: players, console
    aliases: bc
    description: Fais une annonce (30s de cooldown)
    cooldown: 30 seconds	
    cooldown message: &4Tu dois attendre &l%remaining time% &4pour refaire cette commande !
    trigger:
        if player is op:
            if arg-1 is set:
                broadcast "&a&lBREAKING NEWS &7» &6%arg-1%"
                send all players title "&a&lBREAKING NEWS" with subtitle "&6%arg-1%" for 5 seconds
            else:
                send "&4Vous n'avez pas spécifier de texte."
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command skull [<offline player>]: # récupère la tête d'un joueur
    executable by: players
    aliases: head, tete
    description: Te drop une tête d'un joueur
    trigger:
        if player is op:
            set {_player} to player
            if arg-1 is set:
                set {_player} to arg-1
            give {_player}'s skull to player
            send "&7Vous avez reçu la tête de : &6%{_player}%"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command clearchat: # supprime l'historique du tchat
    executable by: players, console
    aliases: cc
    description: Efface l'historique du chat pour tout le monde (1mn de cooldown)
    cooldown: 1 minute
    cooldown message: &4Tu dois attendre &l%remaining time% &4pour refaire cette commande !
    trigger:
        if player is op:
            loop 150 times:
                broadcast " "
            broadcast "&cLe Chat a été clear par &e%player%&c."
            broadcast " "
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command forceexecute <player> <text>: # force un joueur à éxécuter une commande
    executable by: players, console
    aliases: fe
    description: Force une personne à executer une commande
    trigger:
        if player is op:
            execute arg-1 command arg-2
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

# on weather change to rain or thunder: # soleil pour toujours
# 	cancel event

command sethome [<text>]: # défni un home
    executable by: players
    description: Défini un point de téléportation pour ta maison
    trigger:
        if arg-1 isn't set:
            set {_name} to "maison"
        else:
            set {_name} to arg-1
        if {home.%player's uuid%.%{_name}%} is set:
            if arg-1 isn't set:
                set {_name} to ""
            else:
                set {_name} to "%{_name}% "
            send "&4Cette maison existe déjà, &8/delhome %{_name}%&4pour la supprimer."
        else:
            remove {_name} from {home.%player's uuid%::*}
            add {_name} to {home.%player's uuid%::*}
            set {home.%player's uuid%.%{_name}%} to player's location
            send "Ta maison ""&a%{_name}%&r"" est maintenant définie aux coordonnées &7%player's location%&r !"

command delhome [<text>]: # supprime un home
    executable by: players
    description: Supprime le tp vers ta maison
    trigger:
        if arg-1 isn't set:
            set {_name} to "maison"
        else:
            set {_name} to arg-1
        if {home.%player's uuid%.%{_name}%} is set:
            remove {_name} from {home.%player's uuid%::*}
            clear {home.%player's uuid%.%{_name}%}
            send "Maison ""&a%{_name}%&r"" supprimée."
        else:
            send "&4Cette maison n'existe pas."
            make player execute command "listhome"

command home [<text>]: # téléportation vers home
    executable by: players
    description: Te téléportes à ta maison
    trigger:
        if arg-1 isn't set:
            set {_name} to "maison"
        else:
            set {_name} to arg-1
        if {home.%player's uuid%.%{_name}%} is set:
            send "&aTéléportation vers &9%{_name}% &a!"
            teleport player to {home.%player's uuid%.%{_name}%}
        else:
            send "&4Le home ""&c%{_name}%"" &4n'existe pas."
            make player execute command "listhome"

on tab complete for "home" and "delhome": # autocompletion des homes
    if {home.%player's uuid%::*} is set:
        set tab completions for position 1 to the first element of {home.%player's uuid%::*}
        loop {home.%player's uuid%::*}:
            if loop-value isn't the first element of {cards.%player%::*}:
                add loop-value to tab completions

command listhome: # liste tes homes
    executable by: players
    description: Liste des tes maisons
    trigger:
        if {home.%player's uuid%::*} isn't set:
            send "&4Vous n'avez pas de maisons."
        else:
            set {_maisons} to "%{home.%player's uuid%::*}%"
            replace all "and" with "et" in {_maisons}
            send "&aListe des tes maisons : %{_maisons}%"

command rename <string>: # renomme un item
    executable by: players
    description: Renomme l'item que tu as dans ta main
    trigger:
        if player is op:
            if player's tool is air:
                send "&4Tu dois avoir un item dans la main pour le renommer."
            else:
                set {_name} to arg-1
                replace all "&" with "§" in {_name}
                send "&eL'item &r%player's tool%&e est renommé en ""&r%{_name}%&e""."
                set the name of the player's tool to "§r%{_name}%"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command afk: # annonce d'afk
    executable by: players
    description: Annonce que tu vas afk
    trigger:
        broadcast "&a%player's name%&c est désormais afk !"

command gmc [<player>]: # met un joueur en créatif
    executable by: players
    description: Change ton mode de jeu en créatif
    trigger:
        if player is op:
            set {_player} to player
            if arg-1 is set:
                set {_player} to arg-1
            execute {_player} command "gamemode creative"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

command gms [<player>]: # met un joueur en spectateur
    executable by: players
    description: Change ton mode de jeu en spectateur
    trigger:
        if player is op:
            set {_player} to player
            if arg-1 is set:
                set {_player} to arg-1
            execute {_player} command "gamemode spectator"
        else:
            send "&4Tu n'as pas la permission d'utiliser cette commande."

on first join:
    set join message to "&aC'est la première fois que l'on te vois &r%player%, &abienvenue !"

on join:
    set join message to "%player% &aa rejoint le serveur !"

on quit:
    set quit message to "%player% &aa quitté le serveur !"
